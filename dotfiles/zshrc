# Chris Koenig
# ~/.zshrc

# From http://stackoverflow.com/q/4351244, steps to profile this .zshrc:
# Add at the beginning of the file:
#     setopt prompt_subst
#     PS4='+$(date "+%s:%N") %N:%i> '
#     exec 3>&2 2>/tmp/startlog.$$
#     setopt xtrace
# And at the end:
#     unsetopt xtrace
#     exec 2>&3 3>&-

if [[ -d ${HOME}/.oh-my-zsh ]]; then
    export ZSH=$HOME/.oh-my-zsh
    plugins=(git ssh-agent)
    ZSH_THEME="babun"
    source $ZSH/oh-my-zsh.sh
else
    # From https://github.com/MrElendig/dotfiles-alice/blob/master/.zshrc
    setprompt () {
        # load some modules
        autoload -U colors vcs_info zsh/terminfo # Used in the colour alias below

        # http://briancarper.net/blog/570/git-info-in-your-zsh-prompt
        zstyle ':vcs_info:*' stagedstr '%F{28}●'
        zstyle ':vcs_info:*' unstagedstr '%F{yellow}●'
        zstyle ':vcs_info:*' check-for-changes true
        zstyle ':vcs_info:*' enable git
        precmd() {
            if [[ -z $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
                zstyle ':vcs_info:*' formats '[%F{green}%b%c%u%f]'
            else
                zstyle ':vcs_info:*' formats '[%F{green}%b%c%u%F{red}●%f]'
            fi

            vcs_info
        }

        colors
        setopt prompt_subst

        # make some aliases for the colours: (coud use normal escap.seq's too)
        for color in RED GREEN YELLOW BLUE MAGENTA CYAN WHITE; do
            eval PR_$color='%{$fg[${(L)color}]%}'
        done
        PR_NO_COLOR="%{$terminfo[sgr0]%}"

        # Check the UID
        if [[ $UID -ge 1000 ]]; then # normal user
            eval PR_USER='${PR_GREEN}%n${PR_NO_COLOR}'
            eval PR_USER_OP='${PR_GREEN}%#${PR_NO_COLOR}'
        elif [[ $UID -eq 0 ]]; then # root
            eval PR_USER='${PR_RED}%n${PR_NO_COLOR}'
            eval PR_USER_OP='${PR_RED}%#${PR_NO_COLOR}'
        fi

        # Check if we are on SSH or not
        if [[ -n "$SSH_CLIENT"  ||  -n "$SSH2_CLIENT" ]]; then
            eval PR_HOST='${PR_YELLOW}%M${PR_NO_COLOR}' #SSH
        else
            eval PR_HOST='${PR_GREEN}%M${PR_NO_COLOR}' # no SSH
        fi
        # set the prompt
        PS1=$'${PR_NO_COLOR}[${PR_USER}${PR_NO_COLOR}@${PR_HOST}${PR_NO_COLOR}][${PR_BLUE}%~${PR_NO_COLOR}]${vcs_info_msg_0_}${PR_USER_OP}${PR_NO_COLOR} '
        PS2=$'%_>'
    }
    setprompt

    # Enable "ls -G" mode for colorized output on OS X.
    export CLICOLOR=1
fi

# Don't tell me what you thought I typed.
# I'll tell you what I typed. By typing it.
unsetopt correct_all

# If there's a ~/.zsh directory, keep one history file per shell:
if [[ -d "$HOME/.zsh" ]]; then
    HISTFILE=$HOME/.zsh/hist.$$
fi

# Enable vi editing. Only *after* this should we set our other bindkeys.
bindkey -v
# Use control-r to search backwards in history. Useful stuff.
bindkey "^r" history-incremental-search-backward

# No beeping! No beeping.
set nobeep

# Set up a couple aliases
alias vimo="vim -O"
alias ifind="find . -iname"
alias cd..="cd .."

# Tell zsh to print the last command's elapsed time, if greater than 60s.
REPORTTIME=60

# Print out a quote, if the script exists and is executable.
if [[ -x ~/src/ckoenig/bin/motd ]]; then
    ~/src/ckoenig/bin/motd
elif [[ -x ~/src/chrisko/bin/motd ]]; then
    ~/src/chrisko/bin/motd
fi

alias vagrantssh="(cd ~/src/vm && vagrant ssh base)"

alias gdbit='sudo gdb -p $(ps -C xl -o pid=) ~/src/xl/main_debug_build/target/xl/bin/xl'
alias makestart='make -C ~/src/xl WPIDEV=1 XL_VERSION=2.0.0.0 -j8 start'
alias restartit='curl -X POST -d "{ }" http://127.0.0.1/rest/xl/_/restart'
